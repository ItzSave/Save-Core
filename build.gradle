plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

defaultTasks('shadowJar')

group = 'org.itzsave'
version = '1.1.3'
sourceCompatibility = 17

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:$paperVersion"
    compileOnly "me.clip:placeholderapi:$placeholderAPIVersion"
    compileOnly "net.luckperms:api:$luckPermsVersion"
    implementation "me.mattstudios.utils:matt-framework:$commandFrameworkVersion"

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    minimize()
    archiveFileName = "SaveCore-${version}.jar"
    relocate 'me.mattstudios.mf', 'org.itzsave.utils'
}

